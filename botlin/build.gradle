buildscript {
    ext.botlin_version = '0.3.7'

    ext.kotlin_version = '1.3.21'
    ext.kotlin_coroutines_version = '1.1.1'
    ext.spek_version = '1.2.0'

    repositories {
        mavenCentral()
        jcenter()

        maven { url "https://kotlin.bintray.com/kotlinx" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'

        // For spek.gradle
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url 'https://jitpack.io' }
}

group = 'info.mizoguche'
version = botlin_version

apply plugin: 'kotlin'
apply plugin: 'java'
apply plugin: 'com.jfrog.bintray'
apply plugin: "maven-publish"
apply from: 'spek.gradle'

sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
    test.kotlin.srcDirs += 'src/test/kotlin'
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"

    compile 'javax.websocket:javax.websocket-api:1.1'
    compile 'org.glassfish.tyrus.bundles:tyrus-standalone-client:1.13.1'
    compile 'com.github.Ullink:simple-slack-api:1.2.0'
    compile 'redis.clients:jedis:2.9.0'
    compile 'it.sauronsoftware.cron4j:cron4j:2.2.5'
    compile 'org.slf4j:slf4j-simple:1.6.1'
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        name = 'info.mizoguche:botlin'
        licenses = ['MIT']
        websiteUrl = 'https://github.com/mizoguche/botlin'
        vcsUrl = 'https://github.com/mizoguche/botlin.git'
        issueTrackerUrl = 'https://github.com/mizoguche/botlin/issues'

        version {
            name = "$botlin_version"
            desc = 'Bot with Kotlin'
            released = new Date()
            vcsTag = "$botlin_version"
        }
    }
    publications = ['BintrayPublication']
}

publishing {
    publications {
        BintrayPublication(MavenPublication) {
            from components.java
        }
    }
}
