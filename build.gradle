buildscript {
    ext.kotlin_version = '1.1.4-3'
    ext.botlin_version = '0.1.0'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'

        // For spek.gradle
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M4'
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.0.0.M11"
}

detekt {
    version = "1.0.0.M11"
    input = "$input/botlin-core/src/main/kotlin"
    config = "$input/../../../../detekt.yml"
    filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://maven.google.com' }
        maven { url 'https://jitpack.io' }
        maven { url 'https://dl.bintray.com/mizoguche/maven/' }
    }

    group = 'info.mizoguche.botlin'
    version = '0.1.0'
}

configurations {
    ktlint
}

dependencies {
    ktlint 'com.github.shyiko:ktlint:0.8.1'
}

task ktlint(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "botlin-core/src/**/*.kt"
}


task ktlintFormat(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "botlin-core/src/**/*.kt"
}

apply plugin: 'kotlin'
apply plugin: 'java'
apply plugin: 'com.jfrog.bintray'
apply plugin: "maven-publish"

sourceSets {
    main.kotlin.srcDirs += "botlin-core/src/main/kotlin"
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8"
    compile project(":botlin-core")
    compile project(":botlin-example")
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        name = 'info.mizoguche:botlin'
        licenses = ['MIT']
        websiteUrl = 'https://github.com/mizoguche/botlin'
        vcsUrl = 'https://github.com/mizoguche/botlin.git'
        issueTrackerUrl = 'https://github.com/mizoguche/botlin/issues'

        version {
            name = "$botlin_version"
            desc = 'Bot with Kotlin'
            released = new Date()
            vcsTag = "$botlin_version"
        }
    }
    publications = ['BintrayPublication']
}

publishing {
    publications {
        BintrayPublication(MavenPublication) {
            from components.java
        }
    }
}
