buildscript {
    ext.botlin_version = '0.2.2'
    ext.kotlin_version = '1.1.4'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'

        // For spek.gradle
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.0.0.M11"
}

group = 'info.mizoguche'
version = botlin_version

apply plugin: 'kotlin'
apply plugin: 'java'
apply plugin: 'com.jfrog.bintray'
apply plugin: "maven-publish"
apply from: 'spek.gradle'

detekt {
    version = "1.0.0.M11"
    input = "src/main/kotlin"
    config = "detekt.yml"
    filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
}

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://maven.google.com' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://dl.bintray.com/mizoguche/maven/' }
    maven { url "http://dl.bintray.com/kotlin/kotlin-eap-1.1" }
}

configurations {
    ktlint
}

sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
    test.kotlin.srcDirs += 'src/test/kotlin'
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

    compile 'javax.websocket:javax.websocket-api:1.1'
    compile 'org.glassfish.tyrus.bundles:tyrus-standalone-client:1.13.1'
    compile 'com.github.Ullink:simple-slack-api:1.0.0'

    compile 'redis.clients:jedis:2.9.0'

    compile group: 'it.sauronsoftware.cron4j', name: 'cron4j', version: '2.2.5'

    ktlint 'com.github.shyiko:ktlint:0.8.1'

    def coroutines_version = '0.19.1'
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
}

task ktlint(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}


task ktlintFormat(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

sourceSets {
    main.kotlin.srcDirs += "botlin-core/src/main/kotlin"
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        name = 'info.mizoguche:botlin'
        licenses = ['MIT']
        websiteUrl = 'https://github.com/mizoguche/botlin'
        vcsUrl = 'https://github.com/mizoguche/botlin.git'
        issueTrackerUrl = 'https://github.com/mizoguche/botlin/issues'

        version {
            name = "$botlin_version"
            desc = 'Bot with Kotlin'
            released = new Date()
            vcsTag = "$botlin_version"
        }
    }
    publications = ['BintrayPublication']
}

publishing {
    publications {
        BintrayPublication(MavenPublication) {
            from components.java
        }
    }
}
